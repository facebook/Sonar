apply plugin: 'com.android.library'
apply plugin: 'maven'
apply from: 'native.gradle'

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        buildConfigField "boolean", "IS_INTERNAL_BUILD", 'true'

        ndk {
            abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
        }

        externalNativeBuild {
            cmake {
                arguments '-DANDROID_TOOLCHAIN=clang', '-DANDROID_STL=c++_shared'
            }
        }
    }

    lintOptions {
        abortOnError false
    }

    sourceSets {
        main {
            manifest {
                srcFile './AndroidManifest.xml'
            }
            java {
                srcDir 'android'
                srcDir 'core'
                srcDir 'plugins'
                exclude 'console/ConsoleSonarPlugin.java'
                exclude 'console/JavascriptEnvironment.java'
                exclude 'console/JavascriptSession.java'
            }
            res {
                srcDir 'res'
            }
        }
    }

    externalNativeBuild {
        cmake {
            path './CMakeLists.txt'
        }
    }

    dependencies {
        compileOnly deps.lithoAnnotations
        implementation project(':fbjni')
        implementation deps.soloader
        implementation deps.guava
        implementation deps.jsr305
        implementation deps.supportAppCompat
        implementation deps.stetho
        implementation deps.okhttp3
        implementation deps.lithoCore
        implementation deps.lithoWidget
        implementation 'org.mozilla:rhino:1.7.10'
    }
}

project.afterEvaluate {
    preBuild.dependsOn prepareAllLibs
}

apply from: rootProject.file('gradle/release.gradle')

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts.add('archives', sourcesJar)
